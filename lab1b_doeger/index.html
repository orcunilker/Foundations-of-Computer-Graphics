<!DOCTYPE html>
<html lang="en">

<head>
    <meta charset="utf-8">
    <title>Lab1b</title>

    <script type="text/javascript" src="gl-matrix-min.js"></script>
    <script type="text/javascript" src="globals.js"></script>
    <script type="text/javascript" src="ShaderProgram.js"></script>
    <script type="text/javascript" src="objParser.js"></script>
    <script type="text/javascript" src="main.js"></script>
    <script type="text/javascript" src="Shape.js"></script>
    <script type="text/javascript" src="helpers.js"></script>

    <script id="v-shader-nolight" type="x-shader/x-vertex">

        precision mediump float;

        attribute vec4 vertexPosition; 
        attribute vec4 vertexColor;   

        uniform mat4 modelViewMatrix;
        uniform mat4 projectionMatrix;

        varying vec4 fragmentColor;    

        void main() {

            gl_Position = projectionMatrix * modelViewMatrix * vertexPosition; 
            fragmentColor = vertexColor; 

        }

    </script>

    <script id="v-shader-T1" type="x-shader/x-vertex">
        // Gourard Diffuse

        precision mediump float;

        attribute vec4 vertexPosition; 
        attribute vec4 vertexColor;    
        attribute vec3 vertexNormal;

        uniform vec4 lightPointMatrix;
        uniform mat4 modelViewMatrix;
        uniform mat4 viewMatrix;
        uniform mat4 projectionMatrix;
        uniform mat3 normalMatrix;

        varying vec4 fragmentColor;    

        void main() {
    
            // Transform light position to view space
            vec4 lightPosition = viewMatrix * vec4(0.0, 10.0, 0.0, 1.0);
            
            // Transform vertex position to view space
            vec4 viewVertexPosition = modelViewMatrix * vertexPosition;

            // Calculate and normalize light vector
            vec3 lightVector = normalize(lightPosition.xyz - viewVertexPosition.xyz);

            // Transform and normalize the normal
            // (?)Just needed for non-uniform scaling
            vec3 transformedNormal = normalize(normalMatrix * vertexNormal);

            // Calculate light intensity
            // This can be negative, so just make negative values 0
            // Ambient light = 0.1 + Diffuse
            float lightIntensity = 0.1 + max(dot(lightVector, transformedNormal), 0.0);

            gl_Position = projectionMatrix * viewVertexPosition; 

            // Multiply vertex color with lightIntensity
            fragmentColor = vertexColor * lightIntensity;
            
            // set alpha value to 1 again
            fragmentColor.a = 1.0; 
        }

    </script>

    <script id="v-shader-T2" type="x-shader/x-vertex">
        // Gourard Diffuse + Specular

        precision mediump float;

        attribute vec4 vertexPosition; 
        attribute vec4 vertexColor;    
        attribute vec3 vertexNormal;

        uniform mat4 modelViewMatrix;
        uniform mat4 viewMatrix;
        uniform mat4 projectionMatrix;
        uniform mat3 normalMatrix;

        varying vec4 fragmentColor;    

        void main() {
    
            // Transform light position to view space
            vec4 lightPosition = viewMatrix * vec4(0.0, 10.0, 0.0, 1.0);
            
            // Transform vertex position to view space
            vec4 viewVertexPosition = modelViewMatrix * vertexPosition;

            // Calculate and normalize light vector
            vec3 lightVector = normalize(lightPosition.xyz - viewVertexPosition.xyz);

            // Transform and normalize the normal
            // (?)Just needed for non-uniform scaling
            vec3 transformedNormal = normalize(normalMatrix * vertexNormal);

            // Calculate ReflectionVector for Specular
            // r = 2 * (n * l) * n - l (reflect())
            vec3 reflectionVector = normalize(-reflect(lightVector, transformedNormal));
            vec3 eyeVector = normalize(-viewVertexPosition.xyz);

            // Calculate light intensity
            float ambient = 0.1;
            float diffuse = max(dot(lightVector, transformedNormal), 0.0);
            float specular = 0.3 * pow(max(dot(reflectionVector, eyeVector), 0.0), 100.0); //shineniness 100
            float lightIntensity = ambient + diffuse + specular;

            // Multiply vertex color with lightIntensity
            fragmentColor = vertexColor * lightIntensity;
            
            // set alpha value to 1 again
            fragmentColor.a = 1.0; 

            gl_Position = projectionMatrix * viewVertexPosition; 
        }

    </script>

    <script id="f-shader" type="x-shader/x-fragment">
        // For nolight, T1, T2

        precision mediump float;

        varying vec4 fragmentColor;

        void main() {

            gl_FragColor = fragmentColor;

        }

    </script>

    <script id="v-shader-T34" type="x-shader/x-vertex">
        // V-Shader for Phong

        precision mediump float;

        attribute vec4 vertexPosition; 
        attribute vec4 vertexColor;   
        attribute vec3 vertexNormal;

        uniform mat4 modelViewMatrix;
        uniform mat4 viewMatrix;
        uniform mat4 projectionMatrix;
        uniform mat3 normalMatrix;

        varying vec4 fragmentColor; 
        varying vec3 vPosition;
        varying vec3 vNormal;
        varying vec4 viewVertexPosition;

        void main() {
            vec3 transformedNormal = normalize(normalMatrix * vertexNormal);
            vec4 viewVertexPosition = modelViewMatrix * vertexPosition;

            vPosition = vec3(vertexPosition.xyz);
            vNormal = transformedNormal;
            fragmentColor = vertexColor;

            gl_Position = projectionMatrix * viewVertexPosition; 
        }

    </script>


    <script id="f-shader_T3" type="x-shader/x-fragment">
        // Phong Diffuse

        precision mediump float;

        uniform mat4 modelViewMatrix;
        uniform mat4 viewMatrix;

        varying vec4 fragmentColor; 
        varying vec3 vPosition;
        varying vec3 vNormal;

        void main() {

            vec4 viewVertexPosition = modelViewMatrix * vec4(vPosition, 1);
            vec4 lightPosition = viewMatrix * vec4(0.0, 10.0, 0.0, 1.0);
            vec3 normNormal = normalize(vNormal);

            vec3 lightVector = normalize(lightPosition.xyz - viewVertexPosition.xyz);
            float dotLightToNormal = max(dot(lightVector, normNormal), 0.0);

            vec3 diffuse = fragmentColor.rgb * dotLightToNormal;
            float ambient = 0.1;

            gl_FragColor = vec4(ambient + diffuse, 1.0);

        }

    </script>

    <script id="f-shader_T4" type="x-shader/x-fragment">
        // Phong Diffuse + Specular

        precision mediump float;

        uniform mat4 viewMatrix;
        uniform mat4 modelViewMatrix;

        varying vec4 fragmentColor; 
        varying vec3 vPosition;
        varying vec3 vNormal;

        void main() {

            vec4 viewVertexPosition = modelViewMatrix * vec4(vPosition, 1);
            vec4 lightPosition = viewMatrix * vec4(0.0, 10.0, 0.0, 1.0);
            vec3 normNormal = normalize(vNormal);

            vec3 lightVector = normalize(lightPosition.xyz - viewVertexPosition.xyz);
            float dotLightToNormal = max(dot(lightVector, normNormal), 0.0);

            vec3 diffuse = fragmentColor.rgb * dotLightToNormal;
            float ambient = 0.1;

            // Specular
            // ViewVector, perfect Reflection Vector
            vec3 eyeVector = normalize(-viewVertexPosition.xyz);
            vec3 reflectionVector = normalize(-reflect(lightVector, normNormal));
            float specular = pow(max(dot(reflectionVector, eyeVector), 0.0), 100.0); //Shineniness 100

            gl_FragColor = vec4(ambient + diffuse + specular, 1.0);

        }

    </script>

    <style>
        
        #text {
            position: absolute;
            left: 0px;
            top: 0px;
            z-index: 10;
        }
    </style>

</head>

<body>

    <canvas id="canvas" width="1200" height="600"></canvas>
    <canvas id="text"></canvas>

</body>


</html>